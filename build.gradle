plugins {
    id 'java'
    id "com.github.spacialcircumstances.gradle-cucumber-reporting" version "0.1.24"
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of("${java_version}")
    }
}

dependencies {
    implementation 'org.seleniumhq.selenium:selenium-java:4.1.3'
    implementation 'io.github.bonigarcia:webdrivermanager:5.1.0'
    implementation "org.testng:testng:${testngVersion}"
    implementation "io.cucumber:cucumber-java:${cucumberVersion}"
    implementation "io.cucumber:cucumber-testng:${cucumberVersion}"
    implementation "org.slf4j:slf4j-simple:${log4j2_version}"

    implementation "org.projectlombok:lombok:${lombok_version}"
    testImplementation "org.projectlombok:lombok:${lombok_version}"
    annotationProcessor "org.projectlombok:lombok:${lombok_version}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombok_version}"
}

test {
    useTestNG() {
        suites "testng.xml"
        testLogging.showStandardStreams = true
        //report generation delegated to TestNG library:
        useDefaultListeners = true
        outputDirectory = file("$project.buildDir//testngOutput")
    }
    //turn off Gradle's HTML report to avoid replacing the reports generated by TestNG library:
    reports.html.enabled(false)

    systemProperty 'platform', System.getProperty('platform')
}

cucumberReports {
    outputDir = file('build/reports/')
    buildId = '0'
    reports = files('build/cucumber-reports/cucumber.json')
}